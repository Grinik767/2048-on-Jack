class Display {
    function void updateScreen(Board board) {
        var int i, j, score;
        
        var int lineCounter;
        var Tile element;
        var Array xi, display;
        let display = board.getValues();
        let score = board.GetScore();
        let i = 0;
        let lineCounter = 0;
        do Screen.clearScreen();
        do Output.moveCursor(0, 0);
        
        do Display.DrawLine(lineCounter);      //отрисовка верхней горизонтальной линии
        let lineCounter = lineCounter + 1;
        while (i < 4) {     //отрисовка рядов
            do Output.println();
            let j = 0;
            let xi = display[i];
            
            do Output.printString("|   ");
            
            while (j < 4) {         //отрисовка чисел в ряду
                let element = xi[j];
                do element.DrawTile(element.getValue());
                let j = j + 1;
            }
            let i = i + 1;
            do Output.println();
            do Display.DrawLine(lineCounter); //отрисовка горизонтальной линии
            let lineCounter = lineCounter + 1;
            
        }
        do Output.println();
        do Output.println();
        do Output.printString("Score:");
        do Output.printInt(score);
        return;
    }

    
    
    function void DrawLine (int lineCounter) {
        var int horizontalCounter;
        let horizontalCounter = 0;
        if (lineCounter > 0){
            do Output.printString("|      |      |      |      |");
            do Output.println();
        }
        else {
            do Output.println();
        }
        while (horizontalCounter < 29) {        //отрисовка горизонтальной линии
            do Output.printString("-");
            let horizontalCounter = horizontalCounter + 1;
        }
        if (lineCounter < 4) {
            do Output.println();
            do Output.printString("|      |      |      |      |");
        }
        else {
            do Output.println();
        }
        return;
    }
    
    
    
    function void printArray (Array array, int length) {
        var int i;
        var Tile element;
        while (i < length) { 
            let element = array[i];
            do Output.printInt(element.getValue());
            do Output.printString(" ");
            let i = i + 1;
        }
        do Output.println();
        return;
    }
}