class Display {
    function void updateScreen(Board board) {
        var int i, j, score, lineCounter;
        var Tile element;
        var Array xi, display;
        var String s;

        let display = board.getValues();
        let score = board.getScore();
        let i = 0;
        let lineCounter = 0;
        do Screen.clearScreen();
        do Output.moveCursor(0, 0);
        
        let s = "          Game 2048";
        do Output.printString(s);
        do s.dispose();
        do Output.println();
        
        do Display.DrawLine(lineCounter);      //отрисовка верхней горизонтальной линии
        let lineCounter = lineCounter + 1;
        while (i < 4) {     //отрисовка рядов
            do Output.println();
            let j = 0;
            let xi = display[i];
            
            let s = "|  ";
            do Output.printString(s);
            do s.dispose();
            
            while (j < 4) {         //отрисовка чисел в ряду
                let element = xi[j];
                do Display.drawTile(element);
                let j = j + 1;
            }
            let i = i + 1;
            do Output.println();
            do Display.DrawLine(lineCounter); //отрисовка горизонтальной линии
            let lineCounter = lineCounter + 1;
            
        }
        do Output.println();
        do Output.println();
        let s = "Score: ";
        do Output.printString(s);
        do s.dispose();
        do Output.printInt(score);
        return;
    }


    function void DrawLine (int lineCounter) {
        var int horizontalCounter;
        var String s;

        let horizontalCounter = 0;
        if (lineCounter > 0){
            let s = "|      |      |      |      |";
            do Output.printString(s);
            do s.dispose();
            do Output.println();
        }
        else {
            do Output.println();
        }
        let s = "-";
        while (horizontalCounter < 29) {        //отрисовка горизонтальной линии
            do Output.printString(s);
            let horizontalCounter = horizontalCounter + 1;
        }
        do s.dispose();
        if (lineCounter < 4) {
            do Output.println();
            let s = "|      |      |      |      |";
            do Output.printString(s);
            do s.dispose();
        }
        else {
            do Output.println();
        }
        return;
    }

    
    function void printResult () {
        var String s;
        let s = "So good!";
        do Output.println();
        do Output.printString(s);
        do s.dispose();
        
        return;
    }

    function void drawTile (Tile tile) {
        var String s;
        var int value;

        let value = tile.getValue();
        if (value < 10) {
            if (value = 0){
                let s = " ";
                do Output.printString(s);
                do s.dispose();
            } else {
                do Output.printInt(value);
            }
            let s = "   |  ";
            do Output.printString(s);
            do s.dispose();
        }
        if (value > 10) {
            if (value < 100) {
                do Output.printInt(value);
                let s = "  |  ";
                do Output.printString(s);
                do s.dispose();
            }
            else {
                if (value < 1000) {
                    do Output.printInt(value);
                    let s = " |  ";
                    do Output.printString(s);
                    do s.dispose();
                }
                else {
                    do Output.printInt(value);
                    let s = "|  ";
                    do Output.printString(s);
                    do s.dispose();
                }
            }
        }
       
        return;
    }
}